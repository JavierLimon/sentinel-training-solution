# Policy that requires AWS access keys to use PGP keys

##### Imports #####

import "tfplan"
import "strings"

##### Functions #####

# Find all resources of a specific type from all modules using the tfplan import
find_resources_from_plan = func(type) {

  resources = {}

  # Iterate over all modules in the tfplan import
  for tfplan.module_paths as path {
    # Iterate over the named resources of desired type in the module
    for tfplan.module(path).resources[type] else {} as name, instances {
      # Iterate over resource instances
      for instances as index, r {

        # Get the address of the instance
        if length(path) == 0 {
          # root module
          address = type + "." + name + "[" + string(index) + "]"
        } else {
          # non-root module
          address = "module." + strings.join(path, ".module.") + "." +
                    type + "." + name + "[" + string(index) + "]"
        }

        # Add the instance to resources map, setting the key to the address
        resources[address] = r
      }
    }
  }

  return resources
}

# Function to require AWS IAM Access Keys have pgp_key
validate_keys = func() {

  validated = true

  # Get all resource instances of the specified type
  resource_instances = find_resources_from_plan("aws_iam_access_key")

  # Loop through the resource instances
  # Showing 5 solutions with just the first enabled
  for resource_instances as address, r {
    # Validate that access key has PGP key
    # 1
    if r.applied.pgp_key else null not in ["", null] {
    # 2
    # if r.applied.pgp_key else "" not in ["", null] {
    # 3
    # if r.applied.pgp_key else "" is not "" and r.applied.pgp_key else null is not null {
    # 4
    # if r.applied.pgp_key else null is not null and r.applied.pgp_key else "" is not "" {
    # 5 (if you also want to validate that the pgp_key matches "^keybase:(.*)")
    # if r.applied.pgp_key else null not in ["", null] and r.applied.pgp_key matches "^keybase:(.*)" {
    # 6 (if you also want to validate that the pgp_key starts with "keybase:")
    # if r.applied.pgp_key else null not in ["", null] and strings.has_prefix(r.applied.pgp_key, "keybase:") {
      print("Access key", address, "has pgp_key", r.applied.pgp_key)
    } else {
      print("Access key", address, "does not have the pgp_key attribute set")
      validated = false
    } // end if/else
  } //end for

  return validated

}

##### Rules #####

# Call the validation function
keys_validated = validate_keys()

# Main rule that evaluates the result of the validation function
main = rule {
  keys_validated
}
